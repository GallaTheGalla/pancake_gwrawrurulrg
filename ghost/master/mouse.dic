//Separate from the menus because you could put other interactions here, like poking or booping or punching or asking about various hotspots
OnMouseDoubleClick
{
	inmenu = 0
	if reference5 == 0 //reference5 is the mouse button being clicked, in this case a left click
	{
		//reference3 is which character is being clicked
		//If you want to add double click interactions, reference4 will be the name of the hitbox the mouse is over
		if reference3 == 0
		{
			//OnSakuraMenu
			
			if reference4 == "Settings"{
				OnSettingsMenu
			}elseif clicktype == 1{
				OnClick(1)
				--
				"\_q%(tallstack)total pancakes: %TOINT(pancakes)"
			}
			
		}
		else
		{
		
			if reference4 == "Tools"{
				OnToolsMenu
			}elseif reference4 == "Charms"{
				OnUpgradesMenu
			}else{
				OnMainMenu
			}
		}
	}
}

OnNeedlePoke{
	OnClick(1)
	--
	"\_q%(tallstack)total pancakes: %TOINT(pancakes)"
}

OnMouseClick
{
	inmenu = 0
	if reference5 == 0 //reference5 is the mouse button being clicked, in this case a left click
	{
		//reference3 is which character is being clicked
		if reference3 == 0 && reference4 != "Settings"
		{
			//OnSakuraMenu
			
			if clicktype == 0{
				OnClick(1)
				--
				if !checkBalloonOpen; "\_q%(shelf)%(tallstack)\b[6]Total Pancakes: %TOINT(pancakes)"
			}
			
		}
		
	}
}

OnClick{
	pancakes += getTotalClickMult * TOINT(_argv[0])
	

}

OnMouseMove
{
	if reference4 != ""
	{
		stroke++
		
		if stroke % 40 == 0
		{
			if reference3 == 0; OnMouseMove.Sakura
			else; OnMouseMove.Kero
		}
	}
}

OnMouseMove.Sakura
{
	if reference4 == "head"
	{
		"%(tallstack)Petting??"
	}
}

OnMouseMove.Kero
{
	if reference4 == "head"
	{
		"%(shelf)Petting..."
	}
}

OnMouseLeaveAll
{
	stroke = 0
}



OnSecondChange{
	pancakes += getSecCakes
	
	if second % 4 == 0{
	/*
		if inmenu != 0{
			"\C"
		}
		--
		case inmenu{
			when 1{
				"\1\0%(OnMainMenu)\![unlock,balloonrepaint]"
			}
			when 2{
				"\1\0%(OnUpgradesMenu)\![unlock,balloonrepaint]"
			}	
			when 3{
				"\1\0%(OnToolsMenu)\![unlock,balloonrepaint]"
			}
		}
		*/
	}elseif second % 4 == 1{
		checkAchievements
	}elseif second % 4 == 2{
		if inmenu == 0{
			if checkBalloonOpen == 0; "%(shelf)%(tallstack)"
		}
		
	}else{
		OnClick(TOINT(toolsarray[0]))
	}
	

}

getSecCakes{
	_res = 0
	for _i = 1; _i < ARRAYSIZE(ToolsData); _i++{
		_info = ToolsData[_i]
		_res += toolsarray[_i] * TOREAL(_info[3]/10)  * pertoolmult[_i]
	
	}
	_res
}

//Non caller achievements
checkAchievements{
	_database = "ShinyData" + origin
	for _i = 0; _i < ARRAYSIZE(ShinyDataNormal); _i++{
		_thisitem = EVAL("%(_database)[_i]")
		if _thisitem == ""{
			_thisitem = ShinyDataNormal[_i]
		}
		_eval = EVAL(_thisitem[1])
		if _eval && shiniesarray[_i] == 0{
			AchievementPopup(_i)
		}
	}
}



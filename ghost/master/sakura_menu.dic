
//MENUS!

//quick back
nvm{
	"\n[half]You have %TOINT(pancakes) pancakes.\n\n[half]\![*]\q[Nevermind,blank]"	
}

blank{
	inmenu = 0
}

SakuraMenuGreetings : nonoverlap
{
	"Hello."
	"How can I help you?"
}

//Because there is an all modifier here, you don't need to write -- between menu options
OnMainMenu : all
{
	inmenu = 1
	"\C\![lock,balloonrepaint]\c\_q%(tallstack)\b2\![set,autoscroll,disable]"
//	if SHIORI3FW.Eventid == "OnMouseDoubleClick"; SakuraMenuGreetings //The check here is so they won't comment if you click the back button. Remove if you prefer
	
	
	"\![*]\q[Talk to me,OnAiTalk]"
	if LastTalk != ""; "  \![*]\q[Say that again,OnLastTalk]"
	"\n\n"
	"\![*]\q[Tools,OnToolsMenu]\n"	
	"\![*]\q[Shinies,OnShiniesMenu]\n"
	"\![*]\q[Upgrades,OnUpgradesMenu]\n\n"

	
	"\![*]\q[Change Talk Rate,OnTalkRateMenu]       \![*]\q[Settings,OnSettingsMenu]\n\n"
	
	"\![*]\q[Nevermind,blank]"	
	"\n\f[align,center]~~~~~~~~~~~~~~~~~\nPancakes: %TOINT(pancakes) Per Click: %FormatTwoDec(clickmultiplier)\n%FormatTwoDec(getSecCakes) pancakes per sec\n%(phoneFriendAmt(toolsarray[0])) helping click"
	
}

OnChoiceTimeOut{
	inmenu = 0
}

phoneFriendAmt{
	if _argv[0] == 1{
		"1 friend"
		"1 kid"
	}else{
		"%_argv[0] friends"
		"%_argv[0] friends' kids"
	}

}

OnLastTalk
{
	LastTalk
}

OnTalkRateMenu : all
{	
	//This is just setting up a label, you may need to add to it or tweak it if you change what rates are available
	_talkrate = "Off"
	if aitalkinterval == 30; _talkrate = "30 seconds"
	elseif aitalkinterval == 60; _talkrate = "1 minute"
	elseif aitalkinterval == 180; _talkrate = "3 minutes"
	elseif aitalkinterval == 300; _talkrate = "5 minutes"
	elseif aitalkinterval == 600; _talkrate = "10 minutes"
	
	"%(tallstack)\b2\![set,autoscroll,disable]\_q"
	"Current interval: %(_talkrate)\n\n"
	
	"\![*]\q[Off,OnTalkRateChange,0]\n"
	"\![*]\q[30 seconds,OnTalkRateChange,30]\n"
	"\![*]\q[1 minute,OnTalkRateChange,60]\n"
	"\![*]\q[3 minutes,OnTalkRateChange,180]\n"
	"\![*]\q[5 minutes,OnTalkRateChange,300]\n"
	"\![*]\q[10 minutes,OnTalkRateChange,600]\n"
	"\n"
	"\![*]\q[Back,OnSakuraMenu]  \![*]\q[Nevermind,blank]"
}


OnTalkRateChange
{
	aitalkinterval = TOINT(reference0)
}




OnSettingsMenu{
	"\_q"
	--
	"Font Size: \q[ - ,OnDecFont]%fontheight\q[ + ,OnIncFont] \n"
	--
	if clicktype == 0{
		"Click Type: \![*]\q[Single,OnFlipClickType]"
	}else{
		"Click Type: \![*]\q[Double,OnFlipClickType]"
	}
	--
	"\n\n[half]\![*]\q[Reset,OnReset]"
	--
	if incarnations > 0{
		" (You're on your %incarnations run.)"
	}
	--
	"\n%(nvm)     \![*]\q[Back,OnMainMenu]"
	

}

OnDecFont{
	fontheight--
	OnSettingsMenu
}

OnIncFont{
	fontheight++
	OnSettingsMenu
}

OnFlipClickType{
	if clicktype == 0{
		clicktype = 1
	}else{
		clicktype = 0
	}
	OnSettingsMenu
}


OnReset{
	"\_qAre you sure?\n"
	--
	"\n\![*]\q[Reset,OnResetConfirm]"
	--
	"\n\n[half]%(nvm)     \![*]\q[Back,OnMainMenu]"
	
}

OnResetConfirm{
	_incarnations = incarnations
	OnFirstBoot
	incarnations = _incarnations
}












OnUpgradesMenu : all {
	"\C\![lock,balloonrepaint]\c\_q%(tallstack)"
	inmenu = 2
	for _i = 0; _i < ARRAYSIZE(UpgradeData); _i++{
		_thisitem = IARRAY
		_thisitem = UpgradeData[_i]
		//"%_thisitem\n"
		if upgradearray[_i] == 0 {
			//if the user has not bought any
				
			if pancakes >= TOINT(_thisitem[3] * (upgradearray[_i]+1)){
				//can buy it
				"\![*]\q[Buy %(_thisitem[0]),OnBuyUpgrade,%_i] for %TOINT(_thisitem[3]*(upgradearray[_i]+1)) \n"
			}else{
				//broke
				"Buy %(_thisitem[0]) for %TOINT(_thisitem[3]*(upgradearray[_i]+1)) \n"
			}
			
		}else { //bought
			"Owned: %(_thisitem[0])\n"
			
		}
	
	}
	"\n[half]%(nvm)    \![*]\q[Refresh,OnUpgradesMenu]"

}

 

OnBuyUpgrade{
	_index = TOINT(reference0)
	_thisitem = IARRAY
	_thisitem = UpgradeData[_index]
	upgradearray[_index] = 1
	pancakes -= TOINT(_thisitem[3])
	
	//change click multiplier
	clickmultiplier = TOREAL(clickmultiplier + TOREAL(TOREAL(_thisitem[2])/100))
	
	"%(OnUpgradesMenu)\![unlock,balloonrepaint]"
}

OnToolsMenu : all {
	"\C\![lock,balloonrepaint]\c\_q%(tallstack)"
	inmenu = 3
	//HANDLE PHONE
	_thisitem = ToolsData[0]
	if toolsarray[0] > 0 {
		//if the user has at least 1
			
		if pancakes >= TOINT(_thisitem[2] * (toolsarray[0]+1)){
			//can buy more
			"\![*]\q[Phone A Friend,OnPhoneBuy,0] for %TOINT(_thisitem[2]*(toolsarray[0]+1)) (%(toolsarray[0]) Times)\n"
		}else{
			//broke
			"Phone a Friend for %TOINT(_thisitem[2]*(toolsarray[0]+1)) (%(toolsarray[0]) Times)\n"
		}
		
	}elseif pancakes >= TOINT(_thisitem[2]){ //affordable and none
		//not unlocked
			"\![*]\q[Phone A Friend,OnPhoneBuy,0] for %TOINT(_thisitem[2])\n"
		
	}else{
		"Can't Afford To Fix Your Phone\n"
	}
	
	
	//HANDLE REST
	for _i = 1; _i < ARRAYSIZE(ToolsData); _i++{
		_thisitem = ToolsData[_i]
		if toolsarray[_i] > 0 {
			//if the user has at least 1
				
			if pancakes >= TOINT(_thisitem[2] * (toolsarray[_i]+1)){
				//can buy more
				"\![*]\q[Buy %(_thisitem[0]),OnBuyTool,%_i] for %TOINT(_thisitem[2]*(toolsarray[_i]+1)) (%(toolsarray[_i]) Owned)\n"
			}else{
				//broke
				"Buy %(_thisitem[0]) for %TOINT(_thisitem[2]*(toolsarray[_i]+1)) (%(toolsarray[_i]) Owned)\n"
			}
			
		}elseif pancakes >= TOINT(_thisitem[2]){ //affordable and none
			//not unlocked
				"\![*]\q[Buy %(_thisitem[0]),OnBuyTool,%_i] for %TOINT(_thisitem[2])\n"
			
		}else{
			"????\n"
		}
	
	}
	"\n[half]%(nvm)    \![*]\q[Refresh,OnToolsMenu]"

}

OnBuyTool{
	_index = TOINT(reference0)
	_thisitem = ToolsData[_index]
	toolsarray[_index] = (TOINT(toolsarray[_index]) + 1)
	pancakes -= TOINT(_thisitem[2] * toolsarray[_index])
	"%(OnToolsMenu)\![unlock,balloonrepaint]"

}


OnPhoneBuy{
	_index = TOINT(reference0)
	_thisitem = ToolsData[_index]
	toolsarray[_index] = (TOINT(toolsarray[_index]) + 1)
	pancakes -= TOINT(_thisitem[2] * toolsarray[_index])
	
	//random achievements
	if toolsarray[0] == 1 ||toolsarray[0] == 10 ||toolsarray[0] == 100 ||toolsarray[0] == 1000 ||toolsarray[0] == 10000 ||toolsarray[0] == 100000 ||toolsarray[0] == 1000000{
		_rand = RAND(ARRAYSIZE(CallFriendShinyData))
		PhoneAchievement(_rand)
		callfriendarray[_rand] += 1
	
	}
	--
	
	
	"%(OnToolsMenu)\![unlock,balloonrepaint]"

}


PhoneAchievement{
	_index = TOINT(_argv[0])
	
	if callfriendarray[_index] == 0{
		callfriendarray[_index] += 1
		_shiny = CallFriendShinyData[_index]
		"\C%(shelf)Congratulations! You've earned the %(_shiny[0]) shiny!\0\c"
	}
	
}


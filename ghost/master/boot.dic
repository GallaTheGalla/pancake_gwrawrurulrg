OnFirstBoot
{
	//If you add variables, make sure to initialize them here!
	//Note - I haven't added anything about username or pronouns here because it's all handled by the SSP prefs
	stroke = 0
	LastTalk = ""
	
	OnFirstBoot_VARIABLES
	
	"%(shelf)%(tallstack)Take me to your pancakes."
}


OnFirstBoot_VARIABLES{
	pancakes = 0
	totalpancakes = 0
	incarnations = 0
	
	origin = "Normal"
	
	//settings
	clicktype = 0 //0 is single click
	fontheight = "+0" //relative
	goreallowed = 0
	
	columnoverride = 0
	
	clickmultiplier = 1.0
	clickmultbonus = 0.0
	
	upgradearray = initZeroArray(10)
	toolsarray = initZeroArray(10)
	
	shiniesarray = initZeroArray(50)
	upgradelevels = initZeroArray(15)
	
	//fridgemult = 1
	//stovemult = 1
	//wheatmult = 1
	//shootermult = 1
	//botmult = 1
	//circusmult = 1
	//capitalismmult = 1
	
	pertoolmult = initOnesArray(13)
	
	
	
	callfriendarray = initZeroArray(50)
	
	//Decor!
	topping = ""
	syrup = ""
	

}

checkColumnSetup{
	columnoverride || (SHIORI3FW.BalloonName == "Dine-In Breakfast" && TOINT(REPLACE(fontheight,"+","")) <= 2)
}

getTotalClickMult{
	_res = (clickmultbonus + clickmultiplier)
	_res
	
}

OnInitialize
{
	//If you want some variables to reset on boot, put them here. This event will happen even if you add other events like OnGhostChanged
	//OnInitialize is a NOTIFY event! That means it cannot return scripts. This event is only meant for things like variable changes. See this page on Ukadoc for more info: https://ukagakadreamteam.github.io/ukadoc/manual/spec_shiori3.html
	
	//If you add new variables in updates, I recommend initializing them like so:
	//if !ISVAR("NewVariable"); NewVariable = 0
	//This means that it will only set the new variable if the user doesn't already have it, so that their variables don't get overwritten
	
	if reference0 != "reload" //For things you want to reset EXCEPT when just reloading; a convenience for you the dev, basically
	{
		LastTalk = ""
	}
}

OnBoot
{
	_temp = SHIORI3FW.UserBirthday
	_user_birthday = "%(_temp[1])/%(_temp[2])" //M/D, if you want it D/M you can switch it around
	_today = "%(month)/%(day)" //Again, switch this around if using D/M
	
	"%(shelf)%(tallstack)"
	--
	if _today == _user_birthday
	{
		"%(tallstack)It is %(username)'s birthday today.\w8 Happy birthday, %(username).\w8 Developer,\w4 you can add an interesting message here.\w8 It can be as long as you like.\w8 Or, you can remove it entirely."
	}
	elseif _today == "1/1" //You can add whatever cool dates you want with more elseifs here
	{
		"%(tallstack)It's new year's day.\w8 Happy %(year),\w4 %(username)."
	}
	else //Normal boot
	{
		"%(tallstack)Hello %(username).\w8 Good to see you."
		"%(tallstack)Hello.\w8 Ready to make something cool?"
	}
}

OnClose
{
	"%(tallstack)Farewell."
	"%(tallstack)Goodbye."
	--
	"\w8\w8\-" //This is what makes the ghost close (after a short pause), do not remove the \- , and MAKE SURE you don't use a \e before this point or it won't work
}
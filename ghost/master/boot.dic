OnFirstBoot
{
	//If you add variables, make sure to initialize them here!
	//Note - I haven't added anything about username or pronouns here because it's all handled by the SSP prefs
	stroke = 0
	LastTalk = ""
	incarnations = 0
	origin = "Normal"
	handclicks = 0

	//settings
	clicktype = 0 //0 is single click
	fontheight = "+0" //relative
	goreallowed = 0
	billionairehate = 1
	swearing = 0
	shinydialogues = 1
	
	columnoverride = 0
	
	pertoolincarnationmult = initOnesArray(13)
	
	OnFirstBoot_VARIABLES
	
	"%(shelf)%(tallstack)%(nb)It's a typical breakfast time,\w4 and you're in the kitchen.\w8\w8\n\nAfter rummaging through the cabinets,\w4 you decide to make pancakes.\w8\w8\w8\w8\n\nBetter get started.\w8\w8\x"
}

OnFirstBoot_Reset{

	OnFirstBoot_VARIABLES
	_index = TOINT(_argv[0])
	pertoolincarnationmult[_index] = pertoolincarnationmult[_index]  + TOREAL(_argv[1])
	
	origin = randomOrigin
	
	OnBoot
}


randomOrigin : pool {
	"Normal"
	"Supervillain"
	"Grandma"
	"Pancake"
	//	"Alien"
	if incarnations > 4{
	//	"Occult"
		"Raccoon"
		"Rebel"
	//	"Househusband"
	}

}


OnFirstBoot_VARIABLES{
	pancakes = 0
	totalpancakes = 0
	
	incarnations++
	
	lifeinplastic = RAND(8)
	

	
	clickmultiplier = 1.0
	clickmultbonus = 0.0
	
	upgradearray = initZeroArray(10)
	toolsarray = initZeroArray(10)
	
	shiniesarray = initZeroArray(90)
	upgradelevels = initZeroArray(15)
	
	//fridgemult = 1
	//stovemult = 1
	//wheatmult = 1
	//shootermult = 1
	//botmult = 1
	//circusmult = 1
	//capitalismmult = 1
	
	pertoolmult = initOnesArray(13)
	
	
	
	
	
	callfriendarray = initZeroArray(50)
	
	//Decor!
	topping = ""
	syrup = ""
	

}

checkColumnSetup{
	columnoverride || (SHIORI3FW.BalloonName == "Dine-In Breakfast" && TOINT(REPLACE(fontheight,"+","")) <= 2)
}

getTotalClickMult{
	_res = (clickmultbonus + clickmultiplier)
	_res
	
}

OnInitialize
{
	//If you want some variables to reset on boot, put them here. This event will happen even if you add other events like OnGhostChanged
	//OnInitialize is a NOTIFY event! That means it cannot return scripts. This event is only meant for things like variable changes. See this page on Ukadoc for more info: https://ukagakadreamteam.github.io/ukadoc/manual/spec_shiori3.html
	
	//If you add new variables in updates, I recommend initializing them like so:
	//if !ISVAR("NewVariable"); NewVariable = 0
	//This means that it will only set the new variable if the user doesn't already have it, so that their variables don't get overwritten
	
	if reference0 != "reload" //For things you want to reset EXCEPT when just reloading; a convenience for you the dev, basically
	{
		LastTalk = ""
	}
}

OnBoot
{
	"%(shelf)%(tallstack)"
	--
	case origin{
		when "Normal"{
			"%(nb)It's a typical breakfast time,\w4 and you're in the kitchen.\w8\w8\n\nAfter rummaging through the cabinets,\w4 you decide to make pancakes.\w8\w8\w8\w8\n\nBetter get started.\w8\w8\x"
		}
		when "Raccoon"{
			"%(nb)helo\n\w8i am a raecun. \w8i haev decide 2 maek mai family propud. \w8i maed brekkie. everywan love it. \w8so now\w8\w8\w8 i am CHEF. \w8ye. \w8i am CHEFF RAKEOUN. \w8\w8i will maek so mane cakes \w6helo father r u prout of me?? \w8i am chef\nthank yew\w8\x"
		}
		when "Supervillain"{
			"%(nb)It's a typical masterplot scheming time when the thought strikes you:\w8\w8\n\nWhat could be more alluring,\w4 more deceitful,\w4 than pancakes?\w8\w8\n\nIt's the perfect cover,\w4 really.\w8\w8 With pancakes to offer,\w4 who would ever suspect you?\w8\w8\n\nIt's time to do some evil.\w8\w8"
		}
		when "Grandma"{
			"%(nb)It is a wonderful evening, \w6and your grandson has just returned from college for the summer. \w8That's okay though, \w6because you enjoy the company. \w8Now you're actually making meals, \w6too, \w6since you can share them with somebody. \w8You'll do your best.\w4.\w4. so long as he doesn't beat you \f[italics,1]that\f[italics,0] bad in Rummikub. \w8\n\nOkay, \w6let's cook.\w4.\w4. pancakes are a good breakfast!"
		}
		when "Pancake"{
			"%(nb)hello? \w8\w8you've been made. \w8welcome to the world. \w8you are a pancakes. \w8your brethren? \w8brethren.\w8.\w8. pancakes. \w8yes. \w8you're just a pancakes. \w8its a waffle waffle world out there, \w6and you are a pancakes. \w8the pan is where you live. \w8you have now gained opposable thumbs and a third person camera. \w8it is time. \w8you must add to your a pancakes and become the golden brown strength you know you are. \n\n\w8FLIP ME\w8\x"
		}
		when "Rebel"{
			"%(nb)The squad's got a full day of disobediance today,\w4 so let's make sure we're all ready.\w8\w8 Water,\w4 masks,\w4 signs,\w4 burner phones,\w4 all at the ready.\w8\w8\n\nAll that's left is to get everyone nice and fed.\w8\w8 Ain't much better than a good 3 AM pancake.\w8\w8\x"
		}
		
	}
}

OnClose
{
	"%(tallstack)See you on the flipside."
	--
	"\w8\w8\-" //This is what makes the ghost close (after a short pause), do not remove the \- , and MAKE SURE you don't use a \e before this point or it won't work
}